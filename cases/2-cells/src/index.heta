block @Const begin
    plasma = 2.5;
    k_prol = 1e-4;
    AXP_neu =  0.1;
end

namespace invitro begin
    include neu.heta
    tot_vol @Compartment []= 0.1; // <L>

    P1_mean @Record := ( P1_ext * ext + P1_neu * neu_vol ) / tot_vol;

    ext .= 1;
    S1 .= 150; // initial value of substrate
    IL15_ext .= 0; // <nM>
    neu_count .= 1e6;
    P1_ext .= 0;
    P1_neu .= 0;
    ATP_neu .= AXP_neu * K_eq_ADP / (1 + K_eq_ADP);
    ADP_neu .= AXP_neu / (1 + K_eq_ADP);
end

namespace invivo begin
    include neu.heta

    blood @Compartment .= 5.2;
    neu_count @Species {compartment: blood};
    #update S1 {compartment: blood}; // update from neu
    #delete tot_vol; // delete
    ext := plasma; // substitue id is better but is not available for this heta version

    AXP_neu @Record .= 0.1;
    IL15_ext .= 0; // <nM>

    neu_count .= 1e-9;
    r_prol_neu @Reaction {
        actors: -> neu_count
    };
    r_prol_neu := k_prol * neu_count;

    S1 .= 10; // initial value of substrate
    P1_ext .= 0;
    P1_neu .= 0;
    ATP_neu .= AXP_neu * K_eq_ADP / (1 + K_eq_ADP);
    ADP_neu .= AXP_neu / (1 + K_eq_ADP);
end

#export {space: invitro, format: YAML, filepath: yaml_invitro};
#export {space: invivo, format: YAML, filepath: yaml_invivo};
#export {space: invitro, format: SLV, filepath: slv_invitro};
#export {space: invivo, format: SLV, filepath: slv_invivo};
#export {space: invitro, format: Mrgsolve, filepath: mrg_invitro};
#export {space: invivo, format: Mrgsolve, filepath: mrg_invivo};
#export {space: invitro, format: SBML, filepath: sbml_invitro};
#export {space: invivo, format: SBML, filepath: sbml_invivo};

