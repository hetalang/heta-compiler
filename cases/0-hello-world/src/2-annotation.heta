
Vmax @Const = 0.1; // this is global constant available for any scoped equation
Km @Const = 2.5 {units: uM};

// this block unites elements with the same space
namespace mm begin
  '''Note for the Default compartment'''
  default_comp @Compartment { title: Default compartment, units: L };
  default_comp .= 1;

  S @Species { title: substrate, compartment: default_comp, units: uM };
  P @Species { title: product,  compartment: default_comp, units: uM };
  ''' Transformation of __substate__ to __product__ '''
  r1 @Reaction {
    title: Michaelis-Menten reaction,
    actors: S -> P,
    units: umole/min
  };

  r1 := Vmax*S/(Km+S)*default_comp; // S is used here but assigned below
  S .= 10; // S should be initialialized after creation
  P .= 0;
end

mm::mm_sbml @SBMLExport { title: Exported mm model};
mm::mm_simbio @SimbioExport;
mm::full_json @JSONExport;
mm::full_yaml @YAMLExport;
mm::mm_slv @SLVExport;
mm::mm_mrg @MrgsolveExport { title: export to mrgsolve };
