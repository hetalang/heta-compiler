block @Const begin
  kabs = 0.01; // constant of absorption
  kel = 0.01; // constant of elimination
  Vmax = 1.2;
  Km = 1.1;
end

namespace one_comp begin
  a0 @Species {compartment: comp0, isAmount: true};
  s1 @Species {compartment: comp1};
  comp0 @Compartment .= 1;
  comp1 @Compartment .= 5.2;
  vabs @Reaction {actors: a0 -> s1};
  vel @Reaction {actors: s1 ->};

  vabs := kabs*a0;
  vel := kel*s1*comp1;
  a0 []= 10; // initial value
  s1 []= 0;
end

namespace one_comp_saturation begin
  a0 @Species {compartment: comp0, isAmount: true};
  s1 @Species {compartment: comp1};
  comp0 @Compartment .= 1;
  comp1 @Compartment .= 5.2;
  vabs @Reaction {actors: a0 -> s1};
  vel @Reaction {actors: s1 ->};

  vabs := kabs*a0;
  vel := Vmax*s1*comp1/(Km + s1);
  a0 []= 10; // initial value
  s1 []= 0;
end

//one_comp::sbml_non_saturable @SBMLExport;
//one_comp_saturation::sbml_saturable @SBMLExport;
json @JSONExport;
one_comp::slv_non_saturable @SLVExport;
one_comp_saturation::slv_saturable @SLVExport;
one_comp::on_saturable @MrgsolveExport;
one_comp_saturation::mrg_saturable @MrgsolveExport;
