block @Const begin
  comp0 = 1; // compartment 0 volume
  comp1 = 5.2; // compartment 1 volume
  kabs = 0.01; // constant of absorption
  kel = 0.01; // constant of elimination
  Vmax = 1.2;
  Km = 1.1;
end
'''
One compartment PK model
'''
one_comp @model;
block $one_comp begin
  a0 @Species {compartment: comp0, isAmount: true};
  s1 @Species {compartment: comp1};
  comp0 @Compartment;
  comp1 @Compartment;
  vabs @Reaction {actors: a0 -> s1};
  vel @Reaction {actors: s1 ->};

  vabs := kabs*a0;
  vel := kel*s1*comp1;
  a0 []= 10; // initial value
  s1 []= 0;
end

one_comp_saturation @model;
block $one_comp_saturation begin
  a0 @Species {compartment: comp0, isAmount: true};
  s1 @Species {compartment: comp1};
  comp0 @Compartment;
  comp1 @Compartment;
  vabs @Reaction {actors: a0 -> s1};
  vel @Reaction {actors: s1 ->};

  vabs := kabs*a0;
  vel := Vmax*s1*comp1/(Km + s1);
  a0 []= 10; // initial value
  s1 []= 0;
end

sbml_non_saturable @SBMLExport {model: one_comp};
sbml_saturable @SBMLExport {model: one_comp_saturation};
yaml @YAMLExport;
